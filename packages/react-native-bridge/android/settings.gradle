pluginManagement {
    gradle.ext.kotlinVersion = '1.8.0'
    gradle.ext.agpVersion = '8.5.1'
    gradle.ext.automatticPublishToS3Version = '0.8.0'

    plugins {
        id "com.android.library" version gradle.ext.agpVersion
        id "org.jetbrains.kotlin.android" version gradle.ext.kotlinVersion
        id "com.automattic.android.publish-to-s3" version gradle.ext.automatticPublishToS3Version
    }
    repositories {
        maven {
            url 'https://a8c-libs.s3.amazonaws.com/android'
            content {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.publish-to-s3"
            }
        }
        gradlePluginPortal()
        google()
    }
}

rootProject.name = '@wordpress_react-native-bridge'

include ':react-native-bridge'

if (hasProperty("willPublishReactNativeBridgeBinary")) {
    assert file("./react-native-bridge/build/assets/index.android.bundle").exists() : "index.android.bundle is necessary to publish a new version!"
} else {
    include ':@wordpress_react-native-aztec'
    project(':@wordpress_react-native-aztec').projectDir = new File(rootProject.projectDir, '../../react-native-aztec/android')
}
