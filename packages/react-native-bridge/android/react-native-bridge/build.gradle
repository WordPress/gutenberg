import groovy.json.JsonSlurper

plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "maven-publish"
    id "com.automattic.android.publish-to-s3"
}

// import the `readReactNativeVersion()` function
apply from: 'https://gist.githubusercontent.com/hypest/742448b9588b3a0aa580a5e80ae95bdf/raw/8eb62d40ee7a5104d2fcaeff21ce6f29bd93b054/readReactNativeVersion.gradle'

def extractPackageVersion(packagejson, packageName, section) {
    def packageSlurper = new JsonSlurper()
    def packageJson = packageSlurper.parse file(packagejson)
    def dep = packageJson.get(section).get(packageName)

    // Extract version from filename of tarball URL
    def isTarball = dep.endsWith('.tgz')
    if(isTarball) {
        // Replace special characters of package name as "npm pack" command does, to be used in the filename.
        // Reference: https://github.com/npm/cli/blob/699c2d708d2a24b4f495a74974b2a345f33ee08a/lib/pack.js#L66-L67
        def packageNameSanitized = packageName.replace("@", '').replace("/", '-')
        def fileName = dep.substring(dep.lastIndexOf('/') + 1)
        def version = fileName.replace(packageNameSanitized + '-', '').replace('.tgz', '')
        return version
    }

    // Extract version from hash of Git URL
    def version = dep.substring(dep.lastIndexOf('#') + 1)
    return version;
}

group='org.wordpress-mobile.gutenberg-mobile'

def buildAssetsFolder = 'build/assets'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        buildConfigField "boolean", "SHOULD_ATTACH_JS_BUNDLE", willPublishReactNativeBridgeBinary.toString()
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            assets.srcDirs += buildAssetsFolder
            assets.srcDirs += '../../../../../src/block-support'
            // Despite being in a folder called "resources", the files in
            // unsupported-block-editor are accessed as assets by their
            // consumers: the WordPressEditor library.
            assets.srcDirs += '../../../../../resources/unsupported-block-editor'
        }
    }
}

repositories {
    maven { url "https://a8c-libs.s3.amazonaws.com/android" }
    maven { url "https://a8c-libs.s3.amazonaws.com/android/hermes-mirror" }
    maven { url "https://jitpack.io" }
    google()
    mavenCentral()
}

dependencies {
    // For animated GIF support
    implementation 'com.facebook.fresco:animated-gif:2.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "org.wordpress:utils:2.3.0"

    testImplementation "junit:junit:4.13"

    if (willPublishReactNativeBridgeBinary) {
        def rnVersion = readReactNativeVersion('../../../../package.json', 'devDependencies')
        println "react-native version for react-native-bridge: $rnVersion"
        implementation "com.facebook.react:react-native:$rnVersion"
        implementation "org.wordpress-mobile.gutenberg-mobile:react-native-aztec:$reactNativeAztecVersion"
        implementation "com.github.wordpress-mobile:react-native-svg:${extractPackageVersion('../../../react-native-editor/package.json', 'react-native-svg', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-video:${extractPackageVersion('../../../react-native-editor/package.json', 'react-native-video', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-linear-gradient:${extractPackageVersion('../../../react-native-editor/package.json', 'react-native-linear-gradient', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-slider:${extractPackageVersion('../../../react-native-editor/package.json', '@react-native-community/slider', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-get-random-values:${extractPackageVersion('../../../react-native-editor/package.json', 'react-native-get-random-values', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-masked-view:${extractPackageVersion('../../../react-native-editor/package.json', '@react-native-community/masked-view', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-screens:${extractPackageVersion('../../../react-native-editor/package.json', 'react-native-screens', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-safe-area-context:${extractPackageVersion('../../../react-native-editor/package.json', 'react-native-safe-area-context', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-prompt-android:${extractPackageVersion('../../../react-native-editor/package.json', 'react-native-prompt-android', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-webview:${extractPackageVersion('../../../react-native-editor/package.json', 'react-native-webview', 'dependencies')}"
        implementation "com.github.wordpress-mobile:react-native-clipboard:${extractPackageVersion('../../../react-native-editor/package.json', '@react-native-clipboard/clipboard', 'dependencies')}"

        implementation "com.github.fluiddot:publish-gutenberg-mobile-android-dependencies:0.0.1"

        runtimeOnly "org.wordpress-mobile:hermes-release-mirror:0.66.2"
   } else {
        api 'com.facebook.react:react-native:+'
        api project(':@wordpress_react-native-aztec')
        api project(':react-native-linear-gradient')
        api project(':react-native-svg')
        api project(':react-native-video')
        api project(':@react-native-community_slider')
        api project(':react-native-get-random-values')
        api project(':@react-native-community_masked-view')
        api project(':react-native-gesture-handler')
        api project(':react-native-screens')
        api project(':react-native-safe-area-context')
        api project(':react-native-reanimated')
        api project(':react-native-prompt-android')
        api project(':react-native-webview')
        api project(':@react-native-clipboard_clipboard')
        debugRuntimeOnly "org.wordpress-mobile:hermes-debug-mirror:0.66.2"
        releaseRuntimeOnly "org.wordpress-mobile:hermes-release-mirror:0.66.2"
   }
}

project.afterEvaluate {
    publishing {
        publications {
            ReactNativeBridgePublication(MavenPublication) {
                artifact bundleReleaseAar

                groupId 'org.wordpress-mobile.gutenberg-mobile'
                artifactId 'react-native-gutenberg-bridge'
                artifact tasks.named("androidSourcesJar")
                // version is set by 'publish-to-s3' plugin

                addDependenciesToPom(pom)
            }
        }
   }
}

def addDependenciesToPom(pom) {
    pom.withXml {
        def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

        def addDependencies = { configuration, addExtraNode = {} ->
            configuration.allDependencies
                .findAll { it instanceof ExternalDependency && it.name != 'unspecified' }
                .each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                    addExtraNode(dependencyNode)
                }
        }

        addDependencies(configurations.implementation)
        addDependencies(configurations.runtimeOnly) { dependencyNode ->
            dependencyNode.appendNode('scope', 'runtime')
        }
    }
}
